# ***************************************************************************
# *
# *  Copyright (C) 2003-2014
# *  All Rights Reserved.
# *
# ***************************************************************************
# File: Hebrew_Latin_SBL.txt
# Generated from CLDR

# Based on SBL Hebrew transliteration (Academic Style) from
# The SBL Handbook of Style, p. 26
# Academic style is used because it is the more commonly seen
# form in publications.

# Hebrew-Latin/SBL

:: [:Hebrew:];
:: NFD (NFC);

$letter=[̣̂̄́̌̆]*[:L:];

# Combining points unused in SBL transliteration
[ּֽֿׄ֯ ֿׄ֯׳״ּֽ׀׃ ֑֖֛֣֤֥֦֧֪֚֭֮֒֓֔֕֗֘֙֜֝֞֟֠֡֨֩֫֬]+>;
[׳״׀׃]+>\ ;

# Completeness mappings from Latin
|k<c;
|p<f;
|y<j;
|w<v;
|ks<x;

# 4-graph
ָיו<>āyw;

# trigraphs
ֵיּ<>êy;
ֶיּ>êy;
ִיּ<>îy;

# digraphs
ח<>ḥ;
ט<>ṭ;
צ<ṣ}$letter;
צ>ṣ;
ץ<>ṣ;
שׂ<>ś;
שׁ<>š;
ש>š;

ִי<>î;
ָה<>â;
ֵי<>ê;
ֶי>ê;
וֹ<>ô;
וּ<>û;
ְ>ĕ;
ֱ<>ĕ;
ֲ<>ă;
ֳ<>ŏ;
ִ<ī;
ֵ<>ē;
ָ<>ā;
ֹ<>ō;
ֻ<ū;

# unigraphs
א<>ʾ;
ב<>b;
ג<>g;
ד<>d;
ה<>h;
ו<>w;
ז<>z;
י<>y;
כ<k}$letter;
כ>k;
ך<>k;
ל<>l;
מ<m}$letter;
מ>m;
ם<>m;
נ<n}$letter;
נ>n;
ן<>n;
ס<>s;
ע<>ʿ;
פ<p}$letter;
פ>p;
ף<>p;
ק<>q;
ר<>r;
ת<>t;

# Yiddish
װ>ww;
ױ>wy;
ײ>yy;

ִ<>i;
ֶ<>e;
ַ<>a;
ָ<o;
ֻ<>u;

־<>\-;

:: ([A-Z] lower);
:: NFC (NFD);
:: ([[:Latin:][̣̂̄́̌̆\-ʾʿ]]);
